import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
}
apply plugin: 'java'

group = 'com.kwvanderlinde'
// Release versions should be provided by the command line using `-Pversion=x.x.x`
version = (findProperty('version') == 'unspecified') ? 'dev' : version

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

compileJava {
    // This allows parameter names to be visible for ACF.
    options.compilerArgs += ["-parameters"]
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.1-R0.1-SNAPSHOT'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.3'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.8'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'co.aikar', name: 'acf-paper', version: '0.5.0-SNAPSHOT'
    compile group: 'org.nanohttpd', name: 'nanohttpd', version: '2.2.0'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    relocate 'co.aikar.commands', 'com.kwvanderlinde.backupserver.acf'
    relocate 'co.aikar.locales', 'com.kwvanderlinde.backupserver.locales'
}

artifacts {
    shadowJar;
}

// Here is the hack...

build.dependsOn(shadowJar);